apply plugin: 'maven-publish'
//apply plugin: 'signing'
apply from: "${rootDir}/gradle/artifact-settings.gradle"
apply from: "${rootDir}/gradle/publish-root.gradle"

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath = files(android.bootClasspath)
    failOnError = false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.sourceFiles
}

tasks.withType(Javadoc) {
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('docencoding', 'UTF-8')
    options.addStringOption('charset', 'UTF-8')
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}


project.logger.lifecycle(ext["versionName"])

version = project.ext.versionName
group = project.ext.mapLibreArtifactGroupId

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId this.group
                artifactId project.ext.mapLibreArtifactId
                version this.version
                
                from components.release

                artifact androidSourcesJar
                artifact androidJavadocsJar                

                pom {
                    name = project.ext.mapLibreArtifactTitle
                    description = project.ext.mapLibreArtifactTitle
                    url = project.ext.mapLibreArtifactUrl
                    licenses {
                        license {
                            name = project.ext.mapLibreArtifactLicenseName
                            url = project.ext.mapLibreArtifactLicenseUrl
                        }
                    }
                    developers {
                        developer {
                            id = project.ext.mapLibreDeveloperId
                            name = project.ext.mapLibreDeveloperName
                            email = 'maplibre@maplibre.org'
                        }
                    }
                    scm {
                        connection = project.ext.mapLibreArtifactScmUrl
                        developerConnection = project.ext.mapLibreArtifactScmUrl
                        url = project.ext.mapLibreArtifactUrl
                    }
                }
            }
        }
        repositories {
            maven {
                name = "LocalTest2"
                // change to point to your repo, e.g. http://my.org/repo
                url = "file://tmp/000/.m2"
            }

            maven {
                name = "LocalMavenTest"
                // change to point to your repo, e.g. http://my.org/repo
                url = "file://tmp/000/.m2"
            }


        }
    }
}

afterEvaluate { project ->
    android.libraryVariants.all { variant ->
        tasks.androidJavadocs.doFirst {
            classpath += files(variant.javaCompile.classpath.files)
        }
    }
}

//signing {
//    sign publishing.publications
//}

